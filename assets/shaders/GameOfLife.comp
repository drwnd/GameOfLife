#version 460 core

layout (local_size_x = 1, local_size_y = 64, local_size_z = 1) in;

layout (rgba32ui, binding = 0) writeonly restrict uniform uimage2D destination;
layout (rgba32ui, binding = 1) readonly restrict uniform uimage2D source;

uniform int mask;

struct Region {
    uint topLeft;
    uvec4 top;
    uint topRight;
    uint left;
    uvec4 center;
    uint right;
    uint bottomLeft;
    uvec4 bottom;
    uint bottomRight;
};

uvec4 valueAt(ivec2 position) {
    return imageLoad(source, position & ivec2(mask >> 7, mask));
}

Region sampleRegion(ivec2 texelCoord) {
    uint topLeft = valueAt(texelCoord + ivec2(-1, 1)).w;
    uvec4 top = valueAt(texelCoord + ivec2(0, 1));
    uint topRight = valueAt(texelCoord + ivec2(1, 1)).x;

    uint left = valueAt(texelCoord + ivec2(-1, 0)).w;
    uvec4 center = valueAt(texelCoord);
    uint right = valueAt(texelCoord + ivec2(1, 0)).x;

    uint bottomLeft = valueAt(texelCoord + ivec2(-1, -1)).w;
    uvec4 bottom = valueAt(texelCoord + ivec2(0, -1));
    uint bottomRight = valueAt(texelCoord + ivec2(1, -1)).x;

    return Region(topLeft, top, topRight, left, center, right, bottomLeft, bottom, bottomRight);
}

uint getFromRegion(Region region, int x, int y) {
    if (x == -1) {
        if (y == -1) return region.bottomLeft >> 31 & 1;
        if (y == 0) return region.left >> 31 & 1;
        return region.topLeft >> 31 & 1;
    }

    if (x == 128) {
        if (y == -1) return region.bottomRight & 1;
        if (y == 0) return region.right & 1;
        return region.topRight & 1;
    }

    if (y == -1) return region.bottom[x >> 5 & 3] >> x & 1;
    if (y == 0) return region.center[x >> 5 & 3] >> x & 1;
    return region.top[x >> 5 & 3] >> x & 1;
}


void main() {
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
    Region region = sampleRegion(texelCoord);
    uvec4 result = uvec4(0);

    for (int x = 0; x < 128; x++) {
        uint counter = getFromRegion(region, x, 0) << 4;

        counter += getFromRegion(region, x - 1, 1);
        counter += getFromRegion(region, x + 0, 1);
        counter += getFromRegion(region, x + 1, 1);

        counter += getFromRegion(region, x - 1, 0);
        counter += getFromRegion(region, x + 1, 0);

        counter += getFromRegion(region, x - 1, -1);
        counter += getFromRegion(region, x + 0, -1);
        counter += getFromRegion(region, x + 1, -1);

        result[x >> 5 & 3] |= (0xc0008 >> counter & 1) << x;
    }

    imageStore(destination, texelCoord, result);
}